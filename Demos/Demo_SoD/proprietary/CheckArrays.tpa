// Never Forget: Comments can "lie", but code not so much ;P
// CHECKS ARRAYS BEFORE
// -------------------
// Init flag for checking
OUTER_SET failed = 0
// Takes name from array and compares it with the other names
OUTER_FOR(voxIndex = 0; voxIndex < nameNumbers; ++voxIndex) 
BEGIN
	// This is the name value which will be compared with the following names in array
	OUTER_SPRINT previousVox $~%voxNameArray%~(~%voxIndex%~)
	
	// Checks if the all current names in array are subset of previous name 
	ACTION_IF ( (voxIndex+1) <= (nameNumbers - 1) ) THEN
	BEGIN
		OUTER_FOR(innerIndex = voxIndex+1; innerIndex < nameNumbers; ++innerIndex) 
		BEGIN
			OUTER_SPRINT currentVox $~%voxNameArray%~(~%innerIndex%~)	

			// If subset is found then print warning
			ACTION_IF ( (~%currentVox%~ STRING_COMPARE_REGEXP ~^%previousVox%~) = 0 OR (~%previousVox%~ STRING_COMPARE_REGEXP ~^%currentVox%~) = 0 ) THEN
			BEGIN
				WARN ~FOUND SUBSET: In "CheckArrays.tpa" -> Found voice name subset in "%currentVox%" and "%previousVox%"~
				OUTER_SET failed = 1
			END
		END // End of inner for loop

	END // End of first "ACTION_IF"

END // End of outer for loop

// Stops installation and prints a fail message
ACTION_IF (failed = 1) THEN
BEGIN
	FAIL ~In "CheckArrays.tpa" -> There are one or more voice name subsets in %voxNameArray%. Please, read the logfile for more information.~
END